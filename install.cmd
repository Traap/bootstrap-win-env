@echo off
:: {{{ Initial greeting.

echo ========================================
echo üöÄ Setting up Windows 11 Dev Environment
echo ========================================

:: Enable UTF-8
chcp 65001

:: --------------------------------------------------------------------------}}}
:: {{{ Global winget install flags

set WINGET_INSTALL=winget install -e --accept-package-agreements --accept-source-agreements --id

:: --------------------------------------------------------------------------}}}
:: {{{ Update Winget Sources


echo üîÑ Updating winget sources...
winget source update
echo.

:: --------------------------------------------------------------------------}}}
:: {{{ Install programs using winget

echo üß¨ Install in Git...
call :winget_with_prompt Git.Git

echo üíé Installing Ruby...
call :winget_with_prompt RubyInstallerTeam.RubyWithDevKit.3.2

echo üìÑ Installing LaTeX (MiKTeX)...
call :winget_with_prompt MiKTeX.MiKTeX

echo üß† Installing Neovim...
call :winget_with_prompt Neovim.Neovim

echo üìò Installing SumatraPDF...
call :winget_with_prompt SumatraPDF.SumatraPDF

echo üñ•Ô∏è Installing Ast-Grep...
call :winget_with_prompt ast-grep.ast-grep

echo üñ•Ô∏è Installing bat...
call :winget_with_prompt sharkdp.bat

echo üñ•Ô∏è Installing Clink...
call :winget_with_prompt chrisant996.Clink

echo üñ•Ô∏è Installing Flameshot...
call :winget_with_prompt Flameshot.Flameshot

echo üñ•Ô∏è Installing fastfetch...
call :winget_with_prompt Fastfetch-cli.Fastfetch

echo üñ•Ô∏è Installing fd...
call :winget_with_prompt sharkdp.fd

echo üñ•Ô∏è Installing fzf...
call :winget_with_prompt junegunn.fzf

echo üñ•Ô∏è Installing Ghostscript...
call :winget_with_prompt PaperCutSoftware.GhostTrap

echo üñ•Ô∏è Installing GoLang.Go...
call :winget_with_prompt GoLang.Go

echo üñ•Ô∏è Installing ImageMagick...
call :winget_with_prompt ImageMagick.ImageMagick

echo üñ•Ô∏è Installing LazyGit...
call :winget_with_prompt JesseDuffield.lazygit

echo üñ•Ô∏è Installing LuaJIT...
call :winget_with_prompt DEVCOM.LuaJIT

echo üñ•Ô∏è Installing LLVM...
call :winget_with_prompt LLVM.LLVM

echo üñ•Ô∏è Installing Microsoft PowerToys...
call :winget_with_prompt Microsoft.PowerToys

echo üñ•Ô∏è Installing NodeJS...
call :winget_with_prompt OpenJS.NodeJS

echo üñ•Ô∏è Installing ripgrep...
call :winget_with_prompt BurntSushi.ripgrep.MSVC

echo üñ•Ô∏è Installing StrawberryPerl...
call :winget_with_prompt StrawberryPerl.StrawberryPerl

echo üñ•Ô∏è Installing wget...
call :winget_with_prompt GNU.Wget2

echo üñ•Ô∏è Installing w32yank...
call :winget_with_prompt equalsraf.win32yank

echo üñ•Ô∏è Installing Yazi...
call :winget_with_prompt sxyazi.yazi

echo üíé Installing 7zip...
call :winget_with_prompt 7zip.7zip

echo üñ•Ô∏è Installing VS Code...
call :winget_with_prompt Microsoft.VisualStudioCode

:: --------------------------------------------------------------------------}}}
:: {{{ Install VS Code Extensions

:: VS Code CLI path may vary; ensure it's in PATH or use full path
echo üß© Installing VS Code extensions...
call :guarded "VS.Code.Extensions" || goto :SKIP_VS_Code_Extensions
call code --install-extension "rebornix.Ruby"
call code --install-extension "wingrunr21.vscode-ruby"
call code --install-extension "james-yu.latex-workshop"
call code --install-extension "GitHub.vscode-pull-request-github"
call code --install-extension "Neovim.vim"
echo ‚úÖ VS Code extensions installed.
:SKIP_VS_Code_Extensions

:: --------------------------------------------------------------------------}}}
:: {{{ Install Python 3

echo üêç Installing Python 3...
call :guarded "Python.3" || goto :SKIP_Python_3
call :winget_with_prompt Python.Python.3.13
:SKIP_Python_3

:: --------------------------------------------------------------------------}}}
:: {{{ Optional: Install Ruby gems (e.g., rake)

echo üîß Installing Ruby rake gem...
call :guarded "Gem.Rake" || goto :SKIP_Gem_Rake
gem install rake
:SKIP_Gem_Rake

echo üîß Installing Ruby neovim gem...
call :guarded "Gem.Neovim" || goto :SKIP_Gem_Neovim
gem install neovim
:SKIP_Gem_Neovim

:: --------------------------------------------------------------------------}}}
:: {{{ Clone gits CLI

echo üß≠ Cloning gits CLI...
call :guarded "Gits.CLI" || goto :SKIP_GITS_CLI
git clone https://github.com/Traap/gits.git %USERPROFILE%\gits
:SKIP_GITS_CLI

:: --------------------------------------------------------------------------}}}
:: {{{ Set up Python virtual environment

echo üõ†Ô∏è Setting up virtual environment...
call :guarded "Python.virtual.environment" || goto :SKIP_Python_virtual_environment
cd %USERPROFILE%\gits
python -m venv .venv
call .venv\Scripts\activate.bat
:SKIP_Python_virtual_environment

:: --------------------------------------------------------------------------}}}
:: {{{ Install gits dependencies

echo üì¶ Installing Python pip...
call :guarded "Python.Pip" || goto :SKIP_Python_pip
python -m pip install --upgrade pip
pip install .
:SKIP_Python_pip

:: --------------------------------------------------------------------------}}}
:: {{{ Install programs using node

echo üíé Installing neovim host...
call :node_with_prompt neovim

echo üñ•Ô∏è Installing Mermaid CLI
call :node_with_prompt @mermaid-js/mermaid-cli

:: --------------------------------------------------------------------------}}}
:: {{{ Install programs using pip

echo üíé Installing pip...
call :pip_with_prompt pip

echo üñ•Ô∏è Installing pynvim
call :pip_with_prompt pynvim

:: --------------------------------------------------------------------------}}}
:: {{{ Copy repository_locations.yml if it doesn't already exist

echo üìÅ Checking for repository_locations.yml...
call :guarded "Repository.locations" || goto :SKIP_Repository_locations
set "SRC_FILE=%~dp0repository_locations.yml"
set "DEST_DIR=%USERPROFILE%\.config\gits"
set "DEST_FILE=%DEST_DIR%\repository_locations.yml"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"
:SKIP_Repository_locations

:: --------------------------------------------------------------------------}}}
:: {{{ Copy .bashrc it doesn't already exist

echo üìÅ Checking for .bashrc
call :guarded ".bashrc" || goto :SKIP_bashrc
set "DEST_DIR=%USERPROFILE%"

set "SRC_FILE=%~dp0.bash_profile"
set "DEST_FILE=%DEST_DIR%\.bash_profile"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"

set "SRC_FILE=%~dp0.bashrc"
set "DEST_FILE=%DEST_DIR%\.bashrc"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"

set "SRC_FILE=%~dp0.bashrc_personal"
set "DEST_FILE=%DEST_DIR%\.bashrc_personal"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"

set "SRC_FILE=%~dp0.bash_logout"
set "DEST_FILE=%DEST_DIR%\.bash_logout"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"

:SKIP_bashrc

:: --------------------------------------------------------------------------}}}
:: {{{ Copy .inputrc it doesn't already exist

echo üìÅ Checking for .inputrc
call :guarded ".inputrc" || goto :SKIP_inputrc
set "SRC_FILE=%~dp0.inputrc"
set "DEST_DIR=%USERPROFILE%"
set "DEST_FILE=%DEST_DIR%\.inputrc"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"
:SKIP_inputrc

:: --------------------------------------------------------------------------}}}
:: {{{ Copy newdoc it doesn't already exist

echo üìÅ Checking for newdoc
call :guarded "docbld" || goto :SKIP_docbld
set "SRC_FILE=%~dp0docbld"
set "DEST_DIR=%USERPROFILE%\.local\bin"
set "DEST_FILE=%DEST_DIR%\docbld"
call :copy_with_prompt "%SRC_FILE%" "%DEST_DIR%"
:SKIP_docbld

:: --------------------------------------------------------------------------}}}
:: {{{ Final message.

echo ========================================
echo ‚úÖ All steps complete.
echo Please reboot and configure PATH if needed.
echo ========================================
pause
goto :EOF

:: --------------------------------------------------------------------------}}}
:: {{{ Subroutine: winget_with_prompt
::     %1 = winget package ID (e.g., Git.Git)

:winget_with_prompt
set "PKG=%~1"
set /p ANSWER=Install "%PKG%?" [y/N]:
if /I "%ANSWER%"=="N" (
  echo ‚ùå Skipped %PKG%
  echo.
  exit /b
)

echo üõ† Installing %PKG%...
%WINGET_INSTALL% %PKG%
echo.
exit /b

:: --------------------------------------------------------------------------}}}
:: {{{ Subroutine: guarded
::     Usage: call :guarded "Label"
::     Returns 0 to proceed, 1 to skip

:guarded
set "GUARD_LABEL=%~1"

set /p ANSWER=Install "%GUARD_LABEL%"? [y/N]:
if /I "%ANSWER%"=="N" (
  echo ‚ùå Skipped %GUARD_LABEL%
  echo.
  exit /b 1
)

echo ‚úÖ Installing %GUARD_LABEL%...
echo.
exit /b 0

:: --------------------------------------------------------------------------}}}
:: {{{ Subroutine: copy_with_prompt
::     Usage: call copy_with_prompt "src_file", "dest_file", "dest_dir"
::     Returns 0 to proceed, 1 to skip

:copy_with_prompt
setlocal

set "SRC=%~1"
set "DST=%~2"

rem Extract filename only from source path
for %%F in ("%SRC%") do set "FILENAME=%%~nxF"

rem Ensure destination path points to full file, not just directory
if exist "%DST%\" (
  set "DST=%DST%\%FILENAME%"
)

if exist "%DST%" (
  echo ‚ùó %DST% already exists.
  set /p ANSWER=Overwrite with %SRC%? [y/N]:
  if /I "%ANSWER%"=="N" (
    echo ‚ùå Skipped %FILENAME%
    echo.
    endlocal & exit /b 1
  )
)

echo üìù Copying %SRC% ‚Üí %DST%
copy /Y "%SRC%" "%DST%" >nul
echo.
endlocal & exit /b 0

:: --------------------------------------------------------------------------}}}
:: {{{ Subroutine: node_with_prompt
::     %1 = winget package ID (e.g., Git.Git)

:node_with_prompt
set "NODE=%~1"
set /p ANSWER=Install "%NODE%?" [y/N]:
if /I "%ANSWER%"=="N" (
  echo ‚ùå Skipped %NODE%
  echo.
  exit /b
)

echo üõ† Installing %NODE%...
call npm.cmd install -g %NODE%
echo.
exit /b

:: --------------------------------------------------------------------------}}}
:: {{{ Subroutine: pip_with_prompt
::     %1 = winget package ID (e.g., Git.Git)

:pip_with_prompt
set "PIP=%~1"
set /p ANSWER=Install "%PIP%?" [y/N]:
if /I "%ANSWER%"=="N" (
  echo ‚ùå Skipped %PIP%
  echo.
  exit /b
)

echo üõ† Installing %PIP%...
call python -m pip install --upgrade pip
call python -m pip install --upgrade pynvim
echo.
exit /b

:: --------------------------------------------------------------------------}}}
